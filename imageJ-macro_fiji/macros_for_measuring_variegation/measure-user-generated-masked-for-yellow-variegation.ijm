// Joel M. Swenson, LBL, 12-9-15
// Script 1. uses pre-processed images in which a human has determined HSB values that correlated to variegation,
    //	2. measures area of mask, area of fly butt and area of entire image (area of entire image is currently not implemented) and outputs them in that order
    // Note that user should copy and paste output from Fiji to their desired location for numerical analyses
    
    
// Steps used in pre-processing:
// 1. Select fly butt and insert into tiff with black background or if already isolated butt and put in a collage
	// create box around butt and cntrl+shift+d to duplicate it. MAKE SURE YOU SAVE THIS FILE BEFORE PROCEEDING.
// 2. Image -> Adjust -> Color Threshold, This script works if you have the following options: "HSB" for the "Color space" option,
	// Make sure "Dark background" is checked 
	// Make sure that "Pass" is selected for Hue, Saturation and Brightness
	// IMPORTANT NOTE: There appears to be a bug in color threshold, make sure that when Hue limits are 0-255,
	// Saturation limits are 0-255, and Brightness is 1-255 that the entire fly butt is red.
	// If it isn't then restart FIJI. If it is then adjust the brightness max value down to fit your desires.
// 2.5 Press "Select"
// 3. Close "Threshold Color"
// 4. Run this macro. Make sure to copy results before moving on to the next image. Remember that measurements
	// are in the following order: Area of mask measuring variegation, area of fly butt

// Psuedocode of what script does:
// 1. Run measure on user adjusted mask
// 2. Run Color Thresholder (Hue 0-255, Saturation 0-255, Brightness 1-255) and then measure to determine area of fly butt
// 3. If needed in the future, the area of the whole box (Hue 0-255, Saturation 0-255, Brightness 0-255) can be added

run("Clear Results"); // Clear previous results
// run("Select None");


setBatchMode(true); // Controls whether images are visible or hidden during macro execution. If arg is 'true', the interpreter enters batch mode and newly opened images are not displayed. If arg is 'false', exits batch mode and disposes of all hidden images except for the active image, which is displayed in a window. The interpreter also exits batch mode when the macro terminates, disposing of all hidden images.

// Measure area of _variegated_ pixels////////////////////////////////////////////////////

run("Measure");

// Save Tiff of what gets counted as variegated
imgNameOriginal=getTitle();
dir1=getDirectory("image");
run("Duplicate...", "title=1.tif");
//imgNameDuplicate=getTitle();
imgNameDuplicate="1.tif";
selectWindow(imgNameDuplicate);
run("8-bit Color", "number=256");
run("Flatten");
outputPath="/masked_outputs/";
if (!File.exists(dir1+outputPath)) {File.makeDirectory(dir1+outputPath)}; // check if outputPath exists, if not make it
saveAs("Jpeg", dir1+outputPath+imgNameOriginal+"_var-labeled.jpg");
//close();
//selectWindow(imgNameDuplicate);
//close(imgNameDuplicate);


// Measure the area of the fly butt///////////////////////////////////////////////////////
selectWindow(imgNameOriginal);
close("\\Others");
run("Select None");

// Color Thresholder 2.0.0-rc-31/1.49v
// Autogenerated macro, single images only!
min=newArray(3);
max=newArray(3);
filter=newArray(3);
a=getTitle();
run("HSB Stack");
run("Convert Stack to Images");
selectWindow("Hue");
rename("0");
selectWindow("Saturation");
rename("1");
selectWindow("Brightness");
rename("2");
min[0]=0; // Hue min...added by JMS
max[0]=255;
filter[0]="pass";
min[1]=0; // Saturation min...added by JMS
max[1]=255;
filter[1]="pass";
min[2]=1; // Brightness min...added by JMS
max[2]=255;
filter[2]="pass";
for (i=0;i<3;i++){
  selectWindow(""+i);
  setThreshold(min[i], max[i]);
  run("Convert to Mask");
  if (filter[i]=="stop")  run("Invert");
}
imageCalculator("AND create", "0","1");
imageCalculator("AND create", "Result of 0","2");
for (i=0;i<3;i++){
  selectWindow(""+i);
  close();
}
selectWindow("Result of 0");
close();
selectWindow("Result of Result of 0");
rename(a);
// Colour Thresholding-------------


run("Create Selection");
//run("Restore Selection"); 
run("Measure"); 
run("Select None");
close();




